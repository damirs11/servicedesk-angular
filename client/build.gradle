/**
 * Модуль, реализующий front-end, включает стили, логику и графические ресурсы.
 * Модуль полностью отделен от серверного java-кода. Взаимодействие с
 * сервером осуществляется только по согласованному RESTAPI. Никаких jsp, библиотек тэгов и т.д.
 */
plugins {
    //less: https://github.com/ksoichiro/gradle-web-resource-plugin
    id "com.github.ksoichiro.web.resource" version "1.7.3"
    //js min: https://github.com/eriwen/gradle-js-plugin
    id "com.eriwen.gradle.js" version "2.14.1"
}

apply plugin: 'base'

def baseSrcDir = "src/main/"
def baseDstDir = "build/dist/"
def srcDirJs = "$baseSrcDir/js/"
def dstDirJs = "$baseDstDir/js/"

// Компиляция less-файлов
webResource {
    base {
        src = baseSrcDir
        dest = baseDstDir
    }
    less {
        enabled = true
        src = "css"
        dest = "css"
        include = ['style.less', 'bootstrap.less']
        minify = false
        parallelize = true
    }
}

// Объявляем и группируем пути к js-файлам
javascript.source {
    lib {
        js {
            srcDir "$srcDirJs/lib"
            include "*.js"
            exclude "*.min.js"
        }
    }
}

// Объединение нескольких js-файлов в один
combineJs {
    encoding = "UTF-8"
    source = javascript.source.lib.js.files
    dest = file("$dstDirJs/lib/all.js")
}
// Минификация js-файлов
minifyJs {
    source = combineJs
    dest = file("$dstDirJs/lib/all-min.js")
    sourceMap = file("$dstDirJs/lib/all.sourcemap.json")
    closure {
        warningLevel = 'QUIET'
    }
}
// Копирование статичного контента без обработки
task copyStatic(type: Copy) {
    from (baseSrcDir) {
        include 'css/fonts/**'
        include 'img/**'
        include 'js/app/**'
        include 'index.html'
    }
    into baseDstDir
}
//build.dependsOn('webResourceCompileLess', 'copyStatic', 'combineJs', 'minifyJs')
build.dependsOn('webResourceCompileLess', 'copyStatic', 'combineJs')